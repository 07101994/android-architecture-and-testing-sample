apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'jacoco'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.codemate.blogreader"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            testCoverageEnabled = true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors{
        mock {
            applicationIdSuffix = ".mock"
        }
        prod {

        }
    }

    android.variantFilter { variant ->
        if(variant.buildType.name.equals('release')
                && variant.getFlavors().get(0).name.equals('mock')) {
            variant.setIgnore(true);
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }
}

configurations.all {
    resolutionStrategy {
        force 'com.android.support:support-annotations:23.1.1'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // test dependencies
    testCompile jUnit
    testCompile mockito
    testCompile hamcrest

    // Android Testing Support Library's runner and rules
    androidTestCompile supportTest.runner
    androidTestCompile supportTest.rules

    // Espresso UI Testing dependencies.
    androidTestCompile (supportTest.espressoCore) {
        espressoExcludes.each { exclude module : "$it" }
    }

    androidTestCompile (supportTest.espressoContrib) {
        espressoExcludes.each { exclude module : "$it" }
    }

    androidTestCompile (supportTest.espressoIntents) {
        espressoExcludes.each { exclude module : "$it" }
    }

    // app dependencies
    compile support.appcompat
    compile support.design
    compile support.recyclerview
    compile support.cardview

    apt daggerCompiler
    compile dagger
    provided jsr250

    compile retrofit
    compile rxJava
    compile rxAndroid
}